// 
// Created by intellij-pest on 2024-11-05
// grammar
// Author: denisinside
// 

WHITESPACE = _{ " " | "\t" | "\n" }

document = _{ SOI ~ ((!id ~ ANY)* ~ id) ~ (not_apartment? ~ apartment)+ }

not_apartment = _{ (!apartment ~ ANY)* }

apartment = _{
    price
  | address
  | label_section
  | characteristics
  | description
  | details_description
  | rieltor
  | photo_list
}

id = { "Оголошення №" ~ number }

price        =  { "<div class=\"offer-view-price-title\">" ~ price_value ~ "/міс" ~ "</div>" }
price_value  = _{ price_number ~ currency }
price_number =  { (ASCII_DIGIT ~ (ASCII_DIGIT | WHITESPACE)*) }
currency     = @{ "грн" | "€" | "$" }

address      =  { "<div class=\"offer-view-address\">" ~ street ~ "," ~ house_number ~ "</div>" ~ skip_value ~ "</div>" ~ "<div class=\"offer-view-region\">" ~ city ~ "," ~ district }
street       = _{ ("<a class=\"address-link\" href=\"" ~ skip_attr_value ~ "\">" ~ value ~ "</a>") | street_value }
city         = _{ "<a class=\"address-link\" href=\"https://rieltor.ua/flats-rent/\">" ~ value ~ "</a>" }
district     = _{ "<a class=\"address-link\" href=\"" ~ skip_attr_value ~ "\" data-analytics-event=\"card-click-region\">" ~ value ~ "</a>" }
house_number = @{ number ~ ("/" ~ number)? ~ ukr_letter? }
street_value = @{ (!"," ~ ANY)* }

label_section      = { "<div class=\"offer-view-labels uilabels\">" ~ premium_advert_tag? ~ short_period_tag? ~ commission_tag? ~ (subway_station)* ~ (landmark)* ~ residential_complex? ~ allow_children_tag? ~ allow_pets_tag? }
premium_advert_tag = _{ "<span class=\"uilabel -premium\">" ~ premium_advert ~ "</span>" }
premium_advert     = @{ "ПРЕМІУМ"| "ГАРЯЧЕ" | "ЕЛІТ" }
short_period_tag   = _{ "<span class=\"uilabel -green\">" ~ short_period ~ "</span>" }
short_period       = @{ "Доступна на короткий термін" }
commission_tag     = _{ "<span class=\"uilabel -green\">" ~ commission ~ "</span>" }
commission         = ${ (("Комісія " ~ ((number ~ "%") | price_value)) | no_commission) }
no_commission      = @{ "БЕЗ КОМІСІЇ" }
subway_station     =  { "<a class=\"uilabel -link -icon -subway-" ~ subway_line ~ "\"" ~ "href=\"" ~ skip_attr_value ~ "\"" ~ "data-analytics-event=\"card-click-subway_chip\">" ~ skip_svg? ~ "<span>" ~ value ~ "</span>" ~ "</a>" }
subway_line        = @{ "red" | "green" | "blue" }
landmark           =  { "<a class=\"uilabel -link\" data-analytics-event=\"card-click-landmark_chip\"" ~ "href=\"" ~ skip_attr_value ~ "\">" ~ value ~ "</a>" }
residential_complex=  { "<a class=\"uilabel -link\" data-analytics-event=\"card-click-newhouse_chip\"" ~ "href=\"" ~ skip_attr_value ~ "\">" ~ value ~ "</a>" }
allow_children_tag = _{ "<a class=\"uilabel -link\" data-analytics-event=\"card-click-allow_children_chip\"" ~ (!allow_children ~ ANY)* ~ allow_children ~ "</a>" }
allow_children     = @{ "Можна з дітьми" }
allow_pets_tag     = _{ "<a class=\"uilabel -link\" data-analytics-event=\"card-click-allow_pets_chip\"" ~ (!allow_pets ~ ANY)* ~ allow_pets ~ "</a>" }
allow_pets         = @{ "Можна з тваринами" | "Можна з деякими тваринами" }

characteristics =  {
    l_p_char ~ room_count ~ r_p_char ~
    l_p_char ~ area ~ r_p_char ~
    l_p_char ~ floor_info ~ r_p_char ~
    skip_to_date_column ~
    l_p_date_char ~ renewed ~ r_p_char ~
    l_p_date_char ~ published ~ r_p_char ~
    l_p_date_char ~ views ~ r_p_char
}
l_p_char            = _{ ("<div class=\"offer-view-details-column" ~ "\">")? ~ "<div class=\"offer-view-details-row\">" ~ skip_svg? ~ "<span>" }
l_p_date_char       = _{ ("<div class=\"offer-view-details-column-aside" ~ "\">")? ~ "<div class=\"offer-view-details-row\">" ~ skip_svg? ~ "<span>" }
r_p_char            = _{ "</span>" ~ "</div>" ~ "</div>"? }
skip_to_date_column = _{ (!"<div class=\"offer-view-details-column-aside" ~ ANY)* }

room_count = { "<a href=\"" ~ skip_attr_value ~ "\">" ~ number ~ "кімнат" ~ ("а" | "и")? ~ "</a>" }
area       = { float ~ "/" ~ float ~ "/" ~ float ~ "м²" }
floor_info = { "поверх" ~ number ~ "з" ~ number }

renewed    =  { event_date }
published  =  { event_date }
views      =  { number ~ "(сьогодні" ~ number ~ "," ~ "вчора" ~ number ~ ")" }
event_date = @{ "сьогодні" | "вчора" | number ~ (" дні" ~ "в"? | " міс." | " тиж.") ~ " тому" }

description      =  _{ "<div class=\"offer-view-section-title\">Опис</div>" ~ "<div class=\"offer-view-section-text\">" ~ description_text ~ "</div>" }
description_text = @{ (!"</div>" ~ ANY)* }

details_description = { "<div class=\"offer-view-section-title\">Деталі</div>" ~ "<div class=\"offer-view-section-text\">" ~ det_descr_text ~ "</div>" }
det_descr_text      =  { (!"</div>" ~ (not_detail | detail))+ }
not_detail          = _{ (!"</div>" ~ !detail ~ ANY)+ }
detail              = _{ house_type | room_planning | state | bargain }
house_type          = _{ "Будинок -" ~ house_value ~ "," }
room_planning       = _{ "Планування кімнат" ~ planning_value ~ "." }
state               = _{ "Загальний стан квартири -" ~ state_value ~ "." }
bargain             =  { "Торг доречний" }

house_value    = @{ (!"," ~ ANY)+ }
planning_value = @{ (!"." ~ ANY)+ }
state_value    = @{ (!"." ~ ANY)+ }

rieltor              =  { "<div class=\"offer-view-rieltor-header-info\">" ~ rieltor_phone_number ~ rieltor_name ~ rieltor_position ~ rieltor_agency? }
rieltor_phone_number = _{ "<a href=\"https://" ~ phone_number ~ ".rieltor.ua/\" class=\"offer-view-rieltor-name\" rel=\"" ~ skip_attr_value ~ "\">" }
rieltor_name         =  { value ~ "</a>" }
rieltor_position     =  { "<div class=\"offer-view-rieltor-position\">" ~ value ~ "</div>" }
rieltor_agency       =  { "<a href=\"" ~ skip_attr_value ~ "\"" ~ "class=\"offer-view-rieltor-agency-link\">" ~ value ~ "</a>" }
phone_number         = @{ ASCII_DIGIT{10} }

photo_list =  { photo+ }
photo      = _{ "<img class=\"offer-photo-gallery__image\" src=\"" ~ attr_value ~ "\" alt=\"" ~ skip_attr_value ~ "\" loading=\"" ~ skip_attr_value ~ "\">" }

number     = @{ ASCII_DIGIT+ }
float     = @{ number ~ ("." ~ number+)? }
ukr_letter = @{
    ('А'..'Я' | 'а'..'я' | "І" | "і" | "Ї" | "ї" | "Є" | "є" | "Ґ" | "ґ")
}

value      = @{ (!"<" ~ !" "{5} ~ ANY)* }
attr_value =  { (!"\"" ~ ANY)* }

skip_value      = _{ (!"</div>" ~ ANY)* }
skip_attr_value = _{ (!"\"" ~ ANY)* }
skip_svg        = _{ (!"</svg>" ~ ANY)* ~ "</svg>" }
